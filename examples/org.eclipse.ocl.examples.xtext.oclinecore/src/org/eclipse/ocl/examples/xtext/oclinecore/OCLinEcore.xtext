/**
 * <copyright>
 *
 * Copyright (c) 2010,2011 E.D.Willink and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   E.D.Willink - initial API and implementation
 * 	 E.D.Willink (Obeo) - Bug 416287 - tuple-valued constraints
 *
 * </copyright>
 */
grammar org.eclipse.ocl.examples.xtext.oclinecore.OCLinEcore with org.eclipse.ocl.examples.xtext.essentialocl.EssentialOCL

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/ocl/3.1.0/BaseCST" as base
import "http://www.eclipse.org/ocl/3.1.0/EssentialOCLCST" as essentialocl
import "http://www.eclipse.org/ocl/3.1.0/Pivot" as pivot
import "http://www.eclipse.org/ocl/3.1.0/OCLinEcoreCST"
//generate oclinEcore2 "http://www.eclipse.org/ocl/examples/xtext/oclinecore/OCLinEcore"

TopLevelCS :
	('module' name=UnrestrictedName)?
	(ownedLibrary+=LibraryCS)*
	(ownedImport+=ImportCS)*
	(ownedNestedPackage+=PackageCS)*
;

terminal UNQUOTED_STRING:	// Never forward parsed; just provides a placeholder
	'£$%^£$%^'				//  for reverse serialisation of embedded OCL 
;

INTEGER returns ecore::EInt:
	INT
;

EnumerationLiteralName returns ecore::EString:
	EssentialOCLUnrestrictedName
|	'abstract'
|	'attribute'
|	'body'
|	'callable'
|	'class'
|	'composes'
|	'datatype'
|	'definition'
|	'derivation'
|	'derived'
|	'enum'
|	'extends'
|	'id'
|	'import'
|	'initial'
|	'interface'
|	'key'
|	'library'
|	'module'
|	'operation'
|	'ordered'
|	'package'
|	'postcondition'
|	'precondition'
|	'property'
|	'readonly'
|	'reference'
|	'resolve'
|	'static'
|	'super'
|	'throws'
|	'transient'
|	'unique'
|	'unsettable'
|	'volatile'
;

UnrestrictedName returns ecore::EString:
	EnumerationLiteralName
|	'annotation'
|	'documentation'
|	'invariant'
|	'literal'
|	'serializable'
|	'sysml'
;

InvariantConstraintCS returns OCLinEcoreConstraintCS:
	(callable?='callable')? stereotype='invariant' (name=UnrestrictedName ('(' messageSpecification=SpecificationCS ')')?)?
	((':' specification=SpecificationCS? ';') | ';')
;

PostconditionConstraintCS returns OCLinEcoreConstraintCS:
	stereotype='postcondition' (name=UnrestrictedName ('(' messageSpecification=SpecificationCS ')')?)? ':' specification=SpecificationCS? ';'
;

PreconditionConstraintCS returns OCLinEcoreConstraintCS:
	stereotype='precondition' (name=UnrestrictedName ('(' messageSpecification=SpecificationCS ')')?)? ':' specification=SpecificationCS? ';'
;

AnnotationCS returns base::AnnotationCS:
	{base::AnnotationCS} 'annotation' name=(UnrestrictedName|SINGLE_QUOTED_STRING)?
	('(' ownedDetail+=DetailCS (',' ownedDetail+=DetailCS)* ')')?
	(('{' (ownedAnnotation+=AnnotationElementCS
	     | ownedContent+=ModelElementCS
	     | ownedReference+=ModelElementRefCS
	  )+'}')
	|';'
	)
;

AnnotationElementCS returns base::AnnotationElementCS:
	AnnotationCS | DocumentationCS | SysMLCS
;

AttributeCS returns base::AttributeCS:
	((qualifier+='static' (qualifier+='definition')?) | (qualifier+='definition' (qualifier+='static')?))?
	'attribute' name=UnrestrictedName
	(':' ownedType=TypedMultiplicityRefCS)?
	('=' default=SINGLE_QUOTED_STRING)?
	('{'((qualifier+='derived' | qualifier+='!derived' |
		  qualifier+='id' | qualifier+='!id' |
		  qualifier+='ordered' | qualifier+='!ordered' |
		  qualifier+='readonly' | qualifier+='!readonly' |
		  qualifier+='transient' | qualifier+='!transient' |
		  qualifier+='unique' | qualifier+='!unique' |
		  qualifier+='unsettable' | qualifier+='!unsettable' |
		  qualifier+='volatile' | qualifier+='!volatile'
	    ) ','? )+
	'}')?
	(	('{' (ownedAnnotation+=AnnotationElementCS
	      | ('derivation' UnrestrictedName? ':' ownedDefaultExpression+=SpecificationCS? ';') 
	      | ('initial' UnrestrictedName? ':' ownedDefaultExpression+=SpecificationCS? ';') )* '}')
	|	';'
	)
;

ClassCS returns base::ClassCS:
	qualifier+='abstract'?
	'class' name=UnrestrictedName
	(ownedTemplateSignature=TemplateSignatureCS)?
	('extends' ownedSuperType+=TypedRefCS (',' ownedSuperType+=TypedRefCS)*)?
	(':' instanceClassName=SINGLE_QUOTED_STRING)?
	('{' qualifier+='interface'
	 '}')?
	(	('{' (ownedAnnotation+=AnnotationElementCS
	        | ownedOperation+=OperationCS
	        | ownedProperty+=StructuralFeatureCS
	        | ownedConstraint+=InvariantConstraintCS)* '}')
	|	';'
	)
;

ClassifierCS returns base::ClassifierCS:
	ClassCS | DataTypeCS | EnumerationCS 
;

CollectionTypeCS returns essentialocl::CollectionTypeCS:
	name=CollectionTypeIdentifier ('(' ownedType=TypeExpCS ')')?;

DataTypeCS returns base::DataTypeCS:
	'datatype' name=UnrestrictedName
	(ownedTemplateSignature=TemplateSignatureCS)?
	(':' instanceClassName=SINGLE_QUOTED_STRING)?
	('{' (qualifier+='serializable' | qualifier+='!serializable')? '}')?
	(	('{' (ownedAnnotation+=AnnotationElementCS
	        | ownedConstraint+=InvariantConstraintCS)* '}')
	|	';'
	)
;

DetailCS returns base::DetailCS:
	name=(UnrestrictedName|SINGLE_QUOTED_STRING) '=' value+=(SINGLE_QUOTED_STRING|ML_SINGLE_QUOTED_STRING)*
;

DocumentationCS returns base::DocumentationCS:
	{base::DocumentationCS} 'documentation' value=SINGLE_QUOTED_STRING?
	('(' ownedDetail+=DetailCS (',' ownedDetail+=DetailCS)* ')')?
	 ';'
;

EnumerationCS returns base::EnumerationCS:
	'enum' name=UnrestrictedName
	(ownedTemplateSignature=TemplateSignatureCS)?
	(':' instanceClassName=SINGLE_QUOTED_STRING)?
	('{' (qualifier+='serializable' | qualifier+='!serializable')? '}')?
	(	('{' (ownedAnnotation+=AnnotationElementCS
	        | ownedLiterals+=EnumerationLiteralCS
	        | ownedConstraint+=InvariantConstraintCS)* '}')
	| 	';'
	)
;

EnumerationLiteralCS returns base::EnumerationLiteralCS:
	(('literal' name=UnrestrictedName) | name=EnumerationLiteralName) ('=' value=INTEGER)?
	(('{' ownedAnnotation+=AnnotationElementCS* '}')
	|';'
	)
;

ImportCS returns base::ImportCS:
	'import' (name=UnrestrictedName ':')? pathName=URIPathNameCS (all?='::' '*')? ';'
;

LibraryCS returns base::LibraryCS:
	'library' (name=UnrestrictedName ':')? package=[pivot::Package|URI] ';'
;

ModelElementCS returns base::ModelElementCS:
	ClassifierCS | EnumerationLiteralCS | OperationCS | PackageCS | StructuralFeatureCS
;

ModelElementRefCS returns base::ModelElementRefCS:
	'reference' pathName=PathNameCS ';'
;

OperationCS returns base::OperationCS:
	((qualifier+='static' (qualifier+='definition')?) | (qualifier+='definition' (qualifier+='static')?))?
	'operation' (ownedTemplateSignature=TemplateSignatureCS)? name=UnrestrictedName
	'(' (ownedParameter+=ParameterCS (',' ownedParameter+=ParameterCS)*)? ')'
	(':' ownedType=TypedMultiplicityRefCS)?
	('throws' ownedException+=TypedRefCS (',' ownedException+=TypedRefCS)*)?
	('{'((qualifier+='derived' | qualifier+='!derived' |
		  qualifier+='ordered' | qualifier+='!ordered' |
		  qualifier+='unique' | qualifier+='!unique'
		) ','? )+
	'}')?
	(	('{' (ownedAnnotation+=AnnotationElementCS
	        | ownedPrecondition+=PreconditionConstraintCS
	        | ('body' UnrestrictedName? ':' ownedBodyExpression+=SpecificationCS? ';')
	        | ownedPostcondition+=PostconditionConstraintCS)* '}')
	|	';'
	)
;

PackageCS returns base::PackageCS:
	'package' name=UnrestrictedName
	(':' nsPrefix=UnrestrictedName)? ('=' nsURI=URI)?
	(('{'
		(ownedAnnotation+=AnnotationElementCS | ownedNestedPackage+=PackageCS | ownedType+=ClassifierCS)*
		'}')
	|';'
	)
;

ParameterCS returns base::ParameterCS:
	name=UnrestrictedName
	(':' ownedType=TypedMultiplicityRefCS)?
	('{'(( qualifier+='ordered' | qualifier+='!ordered' |
		   qualifier+='unique' | qualifier+='!unique'
		  ) ','?)+
	 '}')?
	('{' ownedAnnotation+=AnnotationElementCS* '}')?
;

ReferenceCS returns base::ReferenceCS:
	((qualifier+='static' (qualifier+='definition')?) | (qualifier+='definition' (qualifier+='static')?))?
	'property' name=UnrestrictedName
	('#' opposite=[pivot::Property|UnrestrictedName])?	
	(':' ownedType=TypedMultiplicityRefCS)?
	('=' default=SINGLE_QUOTED_STRING)?
	('{'((qualifier+='composes' | qualifier+='!composes' |
		  qualifier+='derived' | qualifier+='!derived' |
		  qualifier+='ordered' | qualifier+='!ordered' |
		  qualifier+='readonly' | qualifier+='!readonly' |
		  qualifier+='resolve' | qualifier+='!resolve' |
		  qualifier+='transient' | qualifier+='!transient' |
		  qualifier+='unique' | qualifier+='!unique' |
		  qualifier+='unsettable' | qualifier+='!unsettable' |
		  qualifier+='volatile' | qualifier+='!volatile'
		) ','? )+
	'}')?
	(	('{' (ownedAnnotation+=AnnotationElementCS
			| ('key' keys+=[pivot::Property|UnrestrictedName] (',' keys+=[pivot::Property|UnrestrictedName])* ';')
	        | ('derivation' UnrestrictedName? ':' ownedDefaultExpression+=SpecificationCS? ';') 
	        | ('initial' UnrestrictedName? ':' ownedDefaultExpression+=SpecificationCS? ';') )* '}')
	|	';'
	)
;

SpecificationCS returns essentialocl::ExpSpecificationCS:
	ownedExpression=ExpCS | exprString=UNQUOTED_STRING
;

StructuralFeatureCS returns base::StructuralFeatureCS:
	AttributeCS | ReferenceCS
;

SysMLCS returns SysMLCS:
	{SysMLCS} 'sysml' ((ownedDetail+=DetailCS ';') | ('{' (ownedDetail+=DetailCS ';')* '}'))
;

TemplateBindingCS returns base::TemplateBindingCS:
	('(' ownedParameterSubstitution+=TemplateParameterSubstitutionCS (',' ownedParameterSubstitution+=TemplateParameterSubstitutionCS)* ')')	
|	('<' ownedParameterSubstitution+=TemplateParameterSubstitutionCS (',' ownedParameterSubstitution+=TemplateParameterSubstitutionCS)* '>')	
;

TemplateParameterSubstitutionCS returns base::TemplateParameterSubstitutionCS:
	ownedActualParameter=TypeRefCS	
;

TemplateSignatureCS returns base::TemplateSignatureCS:
	('(' ownedTemplateParameter+=TypeParameterCS (',' ownedTemplateParameter+=TypeParameterCS)* ')')
|	('<' ownedTemplateParameter+=TypeParameterCS (',' ownedTemplateParameter+=TypeParameterCS)* '>')
;

TypeIdentifier:
	UnrestrictedName
|	PrimitiveTypeIdentifier
;

TypeParameterCS returns base::TypeParameterCS:
	name=UnrestrictedName
	( ('extends' ownedExtends+=TypedRefCS ('&&' ownedExtends+=TypedRefCS)*)
	| ('super' ownedSuper=TypedRefCS)
	)?
;

TypeRefCS returns base::TypeRefCS:
	TypedRefCS | WildcardTypeRefCS
;

TypedRefCS returns base::TypedRefCS:
	TypeLiteralCS | TypedTypeRefCS
;

TypedMultiplicityRefCS returns base::TypedRefCS:
	TypedRefCS (multiplicity=MultiplicityCS)?
;

TypedTypeRefCS returns base::TypedTypeRefCS:
	pathName=PathNameCS (ownedTemplateBinding=TemplateBindingCS)?
;

WildcardTypeRefCS returns base::WildcardTypeRefCS:
	{base::WildcardTypeRefCS} '?'
	( ('extends' extends=TypedRefCS)
	| ('super' super=TypedRefCS)
	)?
;