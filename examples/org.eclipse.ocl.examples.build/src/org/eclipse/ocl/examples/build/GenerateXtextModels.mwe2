/**
 * <copyright>
 *
 * Copyright (c) 2010, 2013 E.D.Willink and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *     Adolfo Sanchez-Barbudo Herrera (University of York) - bug397429"
 * </copyright>
 *
 * $Id: GenerateXtextModels.mwe2,v 1.2 2011/01/24 20:54:27 ewillink Exp $
 * 
 * Run As->MWE2 Workflow to regenerate the Xtest CST models.
 */
module GenerateXtextModels

import org.eclipse.ocl.examples.build.*
import org.eclipse.ocl.examples.build.utilities.*
import org.eclipse.ocl.examples.build.xtend.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.ecore.resource.impl.*

/**
 *	Generate the Concrete Syntax Models used by the Xtext editors.
 */
Workflow {
	bean = ResourceSetImpl : resourceSet {}
    bean = StandaloneSetup { resourceSet = resourceSet
    	platformUri = ".."
    	scanClassPath = true
    }
    bean = GenModelSetup { resourceSet = resourceSet }
    bean = UMLGenModelSetup { resourceSet = resourceSet }
    component = GenerateModel { resourceSet = resourceSet skipOnErrors = true
    	genModel = "/org.eclipse.ocl.examples.xtext.markup/model/Markup.genmodel"
    }
    component = @SubGenerateXtextModel { resourceSet = resourceSet
    	projectName = "org.eclipse.ocl.examples.xtext.base"
    	modelName = "baseCST"
    	projectPrefix = "Base"
    	superProjectName = ""
    	superProjectPrefix = ""
		superVisitorPackageName = ""
		superVisitorClassName = ""
		genModelFile = "model/BaseCST.genmodel"
    }
    component = @SubGenerateXtextModel { resourceSet = resourceSet
    	projectName = "org.eclipse.ocl.examples.xtext.essentialocl"
    	modelName = "essentialOCLCST"
    	projectPrefix = "EssentialOCL"
    	superProjectName = "org.eclipse.ocl.examples.xtext.base"
    	superProjectPrefix = "Base"
		genModelFile = "model/EssentialOCLCST.genmodel"
    }
    component = @SubGenerateXtextModel { resourceSet = resourceSet
    	projectName = "org.eclipse.ocl.examples.xtext.oclstdlib"
    	modelName = "oclstdlibCST"
    	projectPrefix = "OCLstdlib"
    	superProjectName = "org.eclipse.ocl.examples.xtext.essentialocl"
    	superProjectPrefix = "EssentialOCL"
		genModelFile = "model/OCLstdlibCST.genmodel"
    }
    component = @SubGenerateXtextModel { resourceSet = resourceSet
    	projectName = "org.eclipse.ocl.examples.xtext.oclinecore"
    	modelName = "oclinEcoreCST"
    	projectPrefix = "OCLinEcore"
    	superProjectName = "org.eclipse.ocl.examples.xtext.essentialocl"
    	superProjectPrefix = "EssentialOCL"
		genModelFile = "model/OCLinEcoreCST.genmodel"
    }
    component = @SubGenerateXtextModel { resourceSet = resourceSet
    	projectName = "org.eclipse.ocl.examples.xtext.completeocl"
    	modelName = "completeOCLCST"
    	projectPrefix = "CompleteOCL"
    	superProjectName = "org.eclipse.ocl.examples.xtext.essentialocl"
    	superProjectPrefix = "EssentialOCL"
		genModelFile = "model/CompleteOCLCST.genmodel"
    }
}
