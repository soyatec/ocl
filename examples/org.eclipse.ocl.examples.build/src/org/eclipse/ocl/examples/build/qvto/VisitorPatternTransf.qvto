// Copyright (c) 2013 Willink Transformations, University of York and others.
// All rights reserved. This program and the accompanying materials
// are made available under the terms of the Eclipse Public License v1.0
// which accompanies this distribution, and is available at
// http://www.eclipse.org/legal/epl-v10.html
// 
// Contributors:
// 		Adolfo Sanchez-Barbudo Herrera (Univerisity of York) - Initial API and implementation
import VisitorPatternLibrary;

modeltype ECORE "strict" uses ecore('http://www.eclipse.org/emf/2002/Ecore');

transformation VisitorPatternTransf(inout packages : ECORE);

configuration property visitorInterfaceName : String;
configuration property visitorInterfaceQualifiedName : String;
configuration property visitableInterfaceName : String;
configuration property visitableInterfaceQualifiedName : String;

main() {

	packages.rootObjects()[EPackage]->forEach(package) {
		var visitorClass := package.findClass(visitorInterfaceName, visitorInterfaceQualifiedName);
		if (visitorClass.oclIsUndefined()) then {
			package.map addVisitorInterface(visitorInterfaceName, visitorInterfaceQualifiedName);
			// FIXME inout mapping doesnt return out result -> Bug ?, try out parameter
			visitorClass := package.findClass(visitorInterfaceName, visitorInterfaceQualifiedName);
		} endif;
		var visitableClass := package.findClass(visitableInterfaceName, visitableInterfaceQualifiedName);
		if (visitableClass.oclIsUndefined()) then {
			package.map addVisitableInterface(visitableInterfaceName, visitableInterfaceQualifiedName);
			// FIXME inout mapping doesnt return out result -> Bug ?, try out parameter
			visitableClass := package.findClass(visitableInterfaceName, visitableInterfaceQualifiedName);
		} endif;
		var packageClasses := package.eClassifiers[EClass]
			->excluding(visitorClass)
			->excluding(visitableClass);
		packageClasses->map weaveAcceptMethod(visitorClass);
		packageClasses->map weaveVisitableInterface(visitableClass);
	}
}

mapping inout EPackage::addVisitorInterface(in visitorName : String, 
		in visitorInstanceClassName : String) // FIXME see above <- : EClass
	when { self.eClassifiers->select(name = visitorName)->isEmpty(); }
{
	var visitor := object EClass {
		name := visitorName;
		instanceClassName := visitorInstanceClassName;
		_abstract := true;
		interface := true;
		eTypeParameters += object ETypeParameter {
			name := "R";
		};
	};
	self.eClassifiers += visitor;
}

mapping inout EPackage::addVisitableInterface(in visitableName : String, 
		in visitableInstanceClassName : String) 
	when { self.eClassifiers->select(name = visitableName)->isEmpty(); }
{
	var visitable := object EClass {
		name := visitableName;
		instanceClassName := visitableInstanceClassName;
		_abstract := true;
		interface := true;
	};
	self.eClassifiers += visitable;
}

mapping inout EClass::weaveAcceptMethod(in visitorClass: EClass) 
	when { self.eOperations->select(name = 'accept')->isEmpty()
			and not self._abstract  
			and not self.interface; } 
{
	var acceptOp := createAcceptMethod(visitorClass);
	acceptOp.eAnnotations += createGenModelAnnotation("return v.visit" + self.name + "(this);");
	eOperations += acceptOp;
}

mapping inout EClass::weaveVisitableInterface(in visitableClass: EClass) 
	when { self.eSuperTypes->isEmpty() } 
{
	self.eSuperTypes += visitableClass;
}
