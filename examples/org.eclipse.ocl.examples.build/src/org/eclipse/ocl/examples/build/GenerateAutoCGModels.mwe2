/**
 * <copyright>
 *
 * Copyright (c) 2013 E.D.Willink and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *
 * </copyright>
 * 
 * Run As->MWE2 Workflow to regenerate the Xtext CG models.
 */
module GenerateAutoCGModels

import org.eclipse.ocl.examples.build.utilities.*
import org.eclipse.ocl.examples.build.xtend.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.ecore.resource.impl.*

/**
 *	Generate the Models and Visitors for the Code Generator.
 */
Workflow {
	bean = ResourceSetImpl : resourceSet {}
    bean = StandaloneSetup { resourceSet = resourceSet
    	platformUri = ".."
    	scanClassPath = true
    }
    bean = GenModelSetup { resourceSet = resourceSet }
    bean = UMLGenModelSetup { resourceSet = resourceSet }
    bean = OCLGenModelSetup {}
	
	component = GenerateModel { resourceSet = resourceSet skipOnErrors = true
		genModel = "/org.eclipse.ocl.examples.autogen/model/autocgmodel.genmodel"
		clearResourceSet = false
	}
    component = GenerateCGVisitors { resourceSet = resourceSet
    	projectName = "org.eclipse.ocl.examples.autogen"
    	projectPrefix = "AutoCGModel"
    	superProjectName = "org.eclipse.ocl.examples.codegen"
    	superProjectPrefix = "CGModel"
    	genModelFile = "model/autocgmodel.genmodel"
    	javaFolder = "emf-gen"
    	modelPackageName = "org.eclipse.ocl.examples.autogen.autocgmodel"
    	visitorPackageName = "org.eclipse.ocl.examples.autogen.autocgmodel.util"
    	visitorClassName = "AutoCGModelVisitor"
    	superVisitorPackageName = "org.eclipse.ocl.examples.codegen.cgmodel.util"
    	superVisitorClassName = "CGModelVisitor"
    	visitablePackageName = "org.eclipse.ocl.examples.codegen.cgmodel"
    	visitableClassName= "CGElement"
    }
}

