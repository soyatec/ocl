/**
 * <copyright>
 *
 * Copyright (c) 2013 E.D.Willink and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 * </copyright>
 *
 * $Id: GenerateXtextModels.mwe2,v 1.2 2011/01/24 20:54:27 ewillink Exp $
 */
module SubGenerateXtextModel

import org.eclipse.emf.mwe.utils.Mapping
import org.eclipse.ocl.examples.build.utilities.GenerateModel
import org.eclipse.ocl.examples.build.utilities.ResourceCopier
import org.eclipse.ocl.examples.build.utilities.ResourceReader
import org.eclipse.ocl.examples.build.utilities.ResourceWriter
import org.eclipse.ocl.examples.build.utilities.ToStringWeaver
import org.eclipse.ocl.examples.build.utilities.VisitorPatternWeaver
import org.eclipse.ocl.examples.build.xtend.GenerateXtextVisitors
import org.eclipse.emf.ecore.resource.ResourceSet

/**
 * A ResourceSet that may be shared by workflow components.
 */
var ResourceSet resourceSet

/**
 * The name of the project hosting the generated models; e.g. "org.eclipse.ocl.examples.xtext.essentialocl"
 */
var projectName

/**
 * The name of the generated model (the package name in the Ecore model); e.g. "essentialOCLCST"
 */
var modelName

/**
 * The prefix of generated model classes; e.g. "EssentialOCL"
 */
var projectPrefix

/**
 * The name of the inherited project hosting the generated models; e.g. "org.eclipse.ocl.examples.xtext.essentialocl", "" for a base visitor.
 */
var superProjectName

/**
 * The prefix of the inherited project hosting the generated models; e.g. "EssentialOCL", "" for a base visitor.
 */
var superProjectPrefix

/**
 * (Optional) The package for generated model interfaces; default: "${projectName}.${modelName}".
 */
var modelPackageName = "${projectName}.${modelName}"  	

/**
 * (Optional) The project-relative genmodel file; default: "model/${projectPrefix}CST.genmodel".
 */
var genModelFile = "model/${projectPrefix}CST.genmodel"

/**
 * (Optional) The Java source folder for generated packages; default: "emf-gen".
 */
var javaFolder = "emf-gen"

/**
 * (Optional) The package of the visitor; default: "${projectName}.util".
 */
var visitorPackageName = "${projectName}.util"

/**
 * (Optional) The class of the visitor; default: "${projectPrefix}CSVisitor".
 */
var visitorClassName = "${projectPrefix}CSVisitor"

/**
 * (Optional) The package of the inherited visitor; default: "${superProjectName}.util". "" for a base visitor.
 */
var superVisitorPackageName = "${superProjectName}.util"

/**
 * (Optional) The class of the inherited visitor; default: "${superProjectPrefix}CSVisitor". "" for a base visitor.
 */
var superVisitorClassName = "${superProjectPrefix}CSVisitor"

/**
 * (Optional) The package of the visitable interface class; default: "org.eclipse.ocl.examples.xtext.base.util"
 */
var visitablePackageName = "org.eclipse.ocl.examples.xtext.base.util"

/**
 * (Optional) The name of the visitable interface class; default: "VisitableCS"
 */
var visitableClassName = "VisitableCS"

/**
 * (Optional) The workspace-relative name of the gen model file; default: "/${projectName}/${genModelFile}"
 */
var genModel = "/${projectName}/${genModelFile}"

var ecoreSlot = "ecore"
/**
 *	Generate a Concrete Syntax Model and its visitors as used by the Xtext editors.
 */
Workflow {
    component = VisitorPatternWeaver { resourceSet = resourceSet skipOnErrors = true
    	inout = "platform:/resource/${projectName}/model-gen/${projectPrefix}CSTImpl.ecore"
    	in = "platform:/resource/org.eclipse.ocl.examples.xtext.base/model-gen/BaseCSTImpl.ecore"
    	visitableInterfaceName = "VisitableCS"
    	visitableInterfaceQualifiedName = "org.eclipse.ocl.examples.xtext.base.util.VisitableCS"
    	rootVisitorInterfaceName = "BaseCSVisitor"
    	rootVisitorInterfaceQualifiedName = "org.eclipse.ocl.examples.xtext.base.util.BaseCSVisitor"
    	visitorInterfaceName = "${projectPrefix}CSVisitor"
    	visitorInterfaceQualifiedName = "${projectName}.util.${projectPrefix}CSVisitor"
    }
    
    component = ToStringWeaver { resourceSet = resourceSet skipOnErrors = true
    	inout = "platform:/resource/${projectName}/model-gen/${projectPrefix}CSTImpl.ecore"
    	in = "http://www.eclipse.org/emf/2002/Ecore"
    	toStringVisitorQualifiedName = "org.eclipse.ocl.examples.xtext.base.utilities.ToStringCS"
    }
    
    component = GenerateModel { resourceSet = resourceSet skipOnErrors = true
    	genModel = genModel
    }
    component = GenerateXtextVisitors { resourceSet = resourceSet skipOnErrors = true
    	projectName = projectName
    	genModelFile = genModelFile
    	javaFolder = javaFolder
    	modelPackageName = modelPackageName    	
    	visitorPackageName = visitorPackageName
    	visitorClassName = visitorClassName
    	superVisitorPackageName = superVisitorPackageName
    	superVisitorClassName = superVisitorClassName
    	visitablePackageName = visitablePackageName
    	visitableClassName = visitableClassName
    }
}
